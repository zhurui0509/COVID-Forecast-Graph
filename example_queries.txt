################# Q1 Which projects have forecasts about [cumulative death] for [California] on [2022-03-12]? When have these forecast been made? Which of them used [regression model]?      #######################

PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX covid-obs-property: <http://covid.geog.ucsb.edu/lod/observedproperty/>
PREFIX covid-instant: <http://covid.geog.ucsb.edu/lod/instant/>
PREFIX covid-place: <http://covid.geog.ucsb.edu/lod/place/>
PREFIX covid: <http://covid.geog.ucsb.edu/lod/ontology/>
PREFIX covid-method: <http://covid.geog.ucsb.edu/lod/method/>

select ?proj_label ?method_label (GROUP_CONCAT(?time; SEPARATOR=",") as ?time_list) where { 
  
  			 ?proj a covid:Research ;
                   rdfs:label ?proj_label ;
                   sosa:hasMember ?f ;
                   sosa:usedProcedure ?method .
  
             ?f a covid:Forecast ;
                sosa:resultTime ?time ;
                sosa:hasMember ?m .
  
             ?m sosa:hasFeatureOfInterest ?p ;
                sosa:observedProperty covid-obs-property:cum_death ;
                sosa:phenomenonTime covid-instant:2022-03-12 .
  
             ?p covid:hasPlaceName 'California' . 
  
  			?method rdfs:label ?method_label ;
                     		    
                                  } 
group by ?proj_label ?method_label


PREFIX covid: <http://covid.geog.ucsb.edu/lod/ontology/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX covid-obs-property: <http://covid.geog.ucsb.edu/lod/observedproperty/>
PREFIX covid-instant: <http://covid.geog.ucsb.edu/lod/instant/>

select ?project_label where {
  ?project a covid:Research ;
           sosa:hasMember ?forecast ;
           rdfs:label ?project_label ;
           sosa:usedProcedure ?method .
    ?method covid:methodType ?method_type .
    ?method_type rdfs:label 'Regression Analysis' .
    
  ?forecast sosa:hasMember ?target ;
      		sosa:resultTime ?time .
  ?target sosa:observedProperty covid-obs-property:cum_death ;
            sosa:phenomenonTime covid-instant:2021-04-10 ;
            sosa:hasFeatureOfInterest ?place.
  ?place covid:hasPlaceName "California" .
}


################## Q2 Which projects implement the assumption that [local social distancing policies will be kept in place]? Which methods do these projects utilize?  ####################

prefix covid: <http://covid.geog.ucsb.edu/lod/ontology/>
prefix covid-assumption-type: <http://covid.geog.ucsb.edu/lod/assumptiontype/> 
prefix sosa: <http://www.w3.org/ns/sosa/> 
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> 

select ?project_label ?method_label  where {

   ?project a covid:Research ;
             rdfs:label  ?project_label ;
             sosa:madeBySensor ?model;
   			 sosa:usedProcedure ?method .
  
     ?model covid:hasAssumption ?assumption ;
            rdfs:label ?model_label . 
  	 ?assumption covid:assumptionType_sd covid-assumption-type:sd_continue .
                 
     ?method covid:methodFull ?method_label . 
  
}


############### Q3  Find all predicted [cumulative death] in [California] on [2021-02-12], and compare it with the reported [ground truth].  ##################

PREFIX covid: <http://covid.geog.ucsb.edu/lod/ontology/>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX covid-obs-property: <http://covid.geog.ucsb.edu/lod/observedproperty/>
PREFIX time: <http://www.w3.org/2006/time#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

select ?forecast ?predict_value ?groundtruth_value where {
   
  ?forecast sosa:hasMember ?target .
  
  ?target a covid:Target ;
            sosa:observedProperty covid-obs-property:cum_death ;
            sosa:phenomenonTime ?time ;
            covid:point ?predict_value ;
            sosa:hasFeatureOfInterest ?place .
    
  ?ground_truth a covid:GroundTruth ;
              sosa:hasFeatureOfInterest ?place ;
              sosa:observedProperty covid-obs-property:cum_death ;
              sosa:phenomenonTime ?time ;
              covid:point ?groundtruth_value .
  
  ?place covid:hasPlaceName "California" .
  ?time time:inXSDDateTime "2022-02-12" .
}
limit 100


PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX covid-obs-property: <http://covid.geog.ucsb.edu/lod/observedproperty/>
PREFIX covid-instant: <http://covid.geog.ucsb.edu/lod/instant/>
PREFIX covid-place: <http://covid.geog.ucsb.edu/lod/place/>
PREFIX covid: <http://covid.geog.ucsb.edu/lod/ontology/>
PREFIX covid-method: <http://covid.geog.ucsb.edu/lod/method/>
PREFIX time: <http://www.w3.org/2006/time#>

select ?project_label ?forecast_time ?ground_value ?upper_bound ?lower_bound ((?upper_bound-?lower_bound) as ?interval) where {
   ?project sosa:hasMember ?forecast ;
            rdfs:label ?project_label . 
    
   ?forecast sosa:hasMember ?target ;
             sosa:resultTime ?forecast_time .
             
   
   ?target a covid:Target ;
            sosa:observedProperty covid-obs-property:cum_death ;
            sosa:phenomenonTime ?time ;
            sosa:hasFeatureOfInterest ?place ;
            covid:quantile_0.01 ?lower_bound ;
            covid:quantile_0.99 ?upper_bound . 
    
  ?ground_truth a covid:GroundTruth ;
              sosa:hasFeatureOfInterest ?place ;
              sosa:observedProperty covid-obs-property:cum_death ;
              sosa:phenomenonTime ?time ;
               covid:point ?ground_value .

  
  ?place covid:hasPlaceName "California" .
  ?time time:inXSDDateTime "2022-02-12" .
    
  filter (?ground_value <?upper_bound && ?ground_value>?lower_bound)
    
}


########### Q4 Among all the 4-week ahead forecasts of cumulative death in early January 2022 (i.e., before the peak of Omicron variant), which model performs the best for each state across the US?  #############
prefix covid: <http://covid.geog.ucsb.edu/lod/ontology/>
prefix covid-instant: <http://covid.geog.ucsb.edu/lod/instant/>
prefix covid-obs-property: <http://covid.geog.ucsb.edu/lod/observedproperty/>
prefix covid-target-type: <http://covid.geog.ucsb.edu/lod/target-type/> 
prefix dcterm: <http://purl.org/dc/terms/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix sosa: <http://www.w3.org/ns/sosa/>
prefix time: <http://www.w3.org/2006/time#> 

select ?research (min(?result_time) as ?first_jan) {
  
  	?research sosa:hasMember ?forecast .
  
  	?forecast a covid:Forecast ;
    	sosa:resultTime ?result_time .

 	FILTER regex(?result_time, "^2022-01") .
}
group by ?research
order by ?first_jan


############# Q5 Rank model performance on predicting cumulative death on 2022-02-05 for Alabama as an example #########

PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX covid-obs-property: <http://covid.geog.ucsb.edu/lod/observedproperty/>
PREFIX covid-instant: <http://covid.geog.ucsb.edu/lod/instant/>
PREFIX covid-place: <http://covid.geog.ucsb.edu/lod/place/>
PREFIX covid: <http://covid.geog.ucsb.edu/lod/ontology/>
PREFIX covid-method: <http://covid.geog.ucsb.edu/lod/method/>
PREFIX time: <http://www.w3.org/2006/time#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

select ?modelName (min(abs(?predict_value-?groundtruth_value)) as ?min_gap) where {
     
  ?target a covid:Target ;
            sosa:observedProperty covid-obs-property:cum_death ;
            sosa:phenomenonTime ?time ;
            covid:point ?predict_value ;
            sosa:hasFeatureOfInterest ?place ;
            ^sosa:hasMember/^sosa:hasMember/sosa:madeBySensor ?model ;
     .
   
  ?model rdfs:label ?modelName .
            
  ?ground_truth a covid:GroundTruth ;
              sosa:hasFeatureOfInterest ?place ;
              sosa:observedProperty covid-obs-property:cum_death ;
              sosa:phenomenonTime ?time ;
              covid:point ?groundtruth_value .
  
  ?place covid:placeFIPS '01' .
    
  ?time time:inXSDDateTime '2022-02-05' .
}
group by ?modelName
order by ?min_gap


############# Q6 What is the relation between [reported incident cases] andcitizens spending time at retail and recreation locations (collected by Google) in [New York]?  #########
PREFIX econ-affinity: <http://econ.geog.ucsb.edu/lod/affinity/>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX econ-obs-property: <http://econ.geog.ucsb.edu/lod/observedproperty/>
PREFIX econ: <http://econ.geog.ucsb.edu/lod/economy/>
PREFIX covid: <http://covid.geog.ucsb.edu/lod/ontology/>
PREFIX covid-obs-property: <http://covid.geog.ucsb.edu/lod/observedproperty/>

SELECT distinct ?time ?groundtruth_value ?val_econ where {

    ?groundtruth a covid:GroundTruth ;
                sosa:phenomenonTime ?time ;
                sosa:observedProperty covid-obs-property:inc_case ;
                 sosa:hasFeatureOfInterest ?place ;
                 covid:point ?groundtruth_value .
  

    econ-affinity:state-daily sosa:hasMember ?econ_collection .
    ?econ_colelction sosa:phenomenonTime ?time ;
                     sosa:hasFeatureOfInterest ?place ;
                     sosa:hasMember ?econ .
    ?econ sosa:observedProperty econ-obs-property:gps_retail_and_recreation ;
      econ:point ?val_econ .

        ?place covid:hasPlaceName "New York" .
}
order by DESC(?time)
limit 100


########## Q7 To find the relation between population density and the accuracy of models in Alabama on 2022-02-12
PREFIX covid: <http://covid.geog.ucsb.edu/lod/ontology/>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX covid-obs-property: <http://covid.geog.ucsb.edu/lod/observedproperty/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX census: <http://econ.geog.ucsb.edu/lod/census/>
PREFIX census-obs-property: <http://covid.geog.ucsb.edu/lod/census_obs_property/>
PREFIX time: <http://www.w3.org/2006/time#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?placeName ?modelName (xsd:float(?census_pop_val)/xsd:float(?census_area_val) AS ?population_density) (min(abs(?predict_value-?groundtruth_value)) as ?min_gap)  where {
	
    ?target a covid:Target ;
            sosa:observedProperty covid-obs-property:cum_death ;
            sosa:phenomenonTime ?time ;
            covid:point ?predict_value ;
            sosa:hasFeatureOfInterest ?place ;
            ^sosa:hasMember/^sosa:hasMember/sosa:madeBySensor ?model ;
     .
    
    ?model rdfs:label ?modelName .

    ?groundtruth a covid:GroundTruth ;
            sosa:phenomenonTime ?time ;
            sosa:observedProperty covid-obs-property:cum_death ;
            sosa:hasFeatureOfInterest ?place ;
            covid:point ?groundtruth_value .
  
    ?census_stat_col a census:StatsCollection ;
            sosa:hasFeatureOfInterest ?place ;
            sosa:hasMember ?census_pop ,
            ?census_area .
    
    ?census_pop covid:point ?census_pop_val;
                sosa:observedProperty census-obs-property:POPESTIMATE2019 . 
    ?census_area covid:point ?census_area_val;
                sosa:observedProperty census-obs-property:TotalArea . 
        
    ?place covid:placeFIPS '01' ;
           covid:hasPlaceName ?placeName.
    
   	?time time:inXSDDateTime '2022-02-12' .

 }
group by ?placeName ?modelName ?census_pop_val ?census_area_val
order by ?min_gap



########################## Below are other testing queries that are not listed in the paper ################################


########### Among all the forecasts on the date of 09-05-2020, which model performs the best for each state across the US?##########


prefix covid: <http://covid.geog.ucsb.edu/lod/ontology/>
prefix covid-instant: <http://covid.geog.ucsb.edu/lod/instant/>
prefix covid-obs-property: <http://covid.geog.ucsb.edu/lod/observedproperty/>
prefix dcterm: <http://purl.org/dc/terms/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix sosa: <http://www.w3.org/ns/sosa/>

select ?placeName ?model_name (min(abs(?sv-?gv)) as ?min_gap) {
	?s a covid:Target ;
		sosa:phenomenonTime covid-instant:2020-09-05 ;
		sosa:observedProperty covid-obs-property:inc_death ;
		sosa:hasFeatureOfInterest ?l ;
		covid:point ?sv ;
		^sosa:hasMember/^sosa:hasMember/sosa:madeBySensor ?model ;
		.

	?model rdfs:label ?model_name .

	?g a covid:GroundTruth ;
		sosa:phenomenonTime covid-instant:2020-09-05 ;
		sosa:observedProperty covid-obs-property:inc_death ;
		sosa:hasFeatureOfInterest ?l ;
		covid:point ?gv ;
		.

	?l covid:hasPlaceName ?placeName .

	values ?placeName {
		"Alabama" "Alaska" "Arizona" "Arkansas" "California" "Colorado" "Connecticut" "Delaware" "Florida"
         "Georgia" "Hawaii" "Idaho" "Illinois" "Indiana" 'Iowa' 'Kansas' 'Kentucky' 'Louisiana' 'Maine'
         'Maryland' 'Massachusetts' 'Michigan' 'Minnesota' 'Mississippi' 'Missouri' 'Montana' 'Nebraska' 'Nevada'
         'New Hampshire' 'New Jersey' 'New Mexico' 'New York' 'North Carolina' 'North Dakota' 'Ohio' 'Oklahoma' 'Oregon'
         'Pennsylvania' 'Rhode Island' 'South Carolina' 'South Dakota' 'Tennessee' 'Texas' 'Utah' 'Vermont' 'Virginia' 
         'Washington' 'West Virginia' 'Wisconsin' 'Wyoming'
	}
}
group by ?model_name ?placeName
order by ?placeName ?min_gap



##################### Compare the number of research that are funded by different organizations such as CDC and NSF.  ######################## Which forecasts are made from independent research (i.e., no funding resource)?  ###########

prefix covid: <http://covid.geog.ucsb.edu/lod/ontology/>
prefix covid-funding-resource: <http://covid.geog.ucsb.edu/lod/fundingResource/>
prefix dcterm: <http://purl.org/dc/terms/>

select ?model_label ?owner_label ?org where {
  ?model a covid:Model ;
      rdfs:label ?model_label ;          
      covid:owner ?owner .
  
  
  ?owner covid:fundingSource ?org ;
          rdfs:label ?owner_label
  #?org rdf:label ?org_label
}



prefix covid: <http://covid.geog.ucsb.edu/lod/ontology/>
prefix covid-assumption-type: <http://covid.geog.ucsb.edu/lod/assumptiontype/> 
prefix sosa: <http://www.w3.org/ns/sosa/> 
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> 


select ?project_label ?model_label ?method_label where {
  
  	 ?project a covid:Research ;
             rdfs:label  ?project_label ;
             sosa:madeBySensor ?model;
   			 sosa:usedProcedure ?method .
                
     ?model covid:hasAssumption ?assumption .
  	 ?assumption covid:assumptionType_sd covid-assumption-type:sd_continue ;
            rdfs:label ?model_label . 
  
     ?method rdfs:label  ?method_label . 
}
